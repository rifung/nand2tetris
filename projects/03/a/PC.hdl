// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment
    Inc16(in=registerOut, out=increment);
    Mux16(a[0..15]=false, b=increment, sel=inc, out=incOut);
    
    // load
    Mux16(a=incOut, b=in, sel=load, out=loadOut);

    // reset
    Mux16(a=loadOut, b[0..15]=false, sel=reset, out=resetOut);

    Or(a=load, b=inc, out=loadOrInc);
    Or(a=loadOrInc, b=reset, out=loadAny);

    Register(in=resetOut, load=loadAny, out=registerOut, out=out);
}
